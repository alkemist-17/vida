
//
// Script: functools
// Provides some high order functions for arrays.
// Version 1.0.0
//



// Library object
var lib = {}



// Map 
lib.map = fun a, f {
    var xs = array(len(a))
    for i, v in a {
        xs[i] = f(v)
    }
    ret xs
}



// Filter
lib.filter = fun a, pred {
    var xs = []
    for _, v in a {
        if pred(v) {
            append(xs, v)
        }
    }
    ret xs
}



// All
lib.all = fun a, pred {
    for _, v in a {
        if not pred(v) {
            ret false
        }
    }
    ret true
}



// Any
lib.any = fun a, pred {
    for _, v in a {
        if pred(v) {
            ret true
        }
    }
    ret false
}



// Reduce
lib.reduce = fun a, f, init {
    for _, v in a {
        init = f(init, v)
    }
    ret init
}



// TakeWhile
lib.takeWhile = fun a, pred {
    var xs = []
    for _, v in a {
        if not pred(v) {
            ret xs
        }
        append(xs, v)
    }
    ret xs
}



// Zip
lib.zip = fun l, r {
    var length = nil
    var ll = len(l)
    var lr = len(r)
    if ll < lr {
        length = ll
    } else {
        length = lr
    }
    var xs = array(length)
    for i in 0, length {
        xs[i] = [l[i], r[i]]
    }
    ret xs
}



// UnZip
lib.unzip = fun zipped {
    var ll = len(zipped)
    var a = array(ll)
    var b = array(ll)
    for i, v in zipped {
        a[i] = v[0]
        b[i] = v[1]
    }
    ret [a, b]
}



export lib
